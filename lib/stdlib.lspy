;==== Lispy Standard Library ===================================================
;---- Functional definitions ---------------------------------------------------

; Open new scope
(fn {let b} {
  ((lambda {_} b) ())
})

;---- Misc functions ----------------------------------------------------------

(fn {flip f a b} {f b a})
(fn {ghost & xs} {eval xs})
(fn {comp f g x} {f (g x)})

;---- List fnctions -----------------------------------------------------------

; Fold Left
(fn {foldl f z l} {
  if (== l nil) 
    {z}
    {foldl f (f z (fst l)) (tail l)}
})

; Fold Right
(fn {foldr f z l} {
  if (== l nil) 
    {z}
    {f (fst l) (foldr f z (tail l))}
})

; Find element in list of pairs
(fn {lookup x l} {
  if (== l nil)
    {error "No Element Found"}
    {do
      (= {key} (fst (fst l)))
      (= {val} (snd (fst l)))
      (if (== key x) {val} {lookup x (tail l)})
    }
})

; Zip two lists together into a list of pairs
; https://github.com/Federico-abss/Lispy/blob/master/std-library/std_library.lspy#L198
(fn {zip x y} {
  if (or (== x nil) (== y nil))
    {nil}
    {join (list (join (head x) (head y))) (zip (tail x) (tail y))}
})

; Unzip a list of pairs into two lists
; https://github.com/Federico-abss/Lispy/blob/master/std-library/std_library.lspy#L204
(fn {unzip l} {
  if (== l nil)
    {{nil nil}}
    {do
      (= {x} (fst l))
      (= {xs} (unzip (tail l)))
      (list (join (head x) (fst xs)) (join (tail x) (snd xs)))
    }
})
